#Использовать yadisk
#Использовать logos
#Использовать cmdline

Перем мЛог;

Процедура ВыполнитьРаботу()

	Если АргументыКоманднойСтроки.Количество() = 0 Тогда
		ВывестиСправку();
		ЗавершитьРаботу(0);
	КонецЕсли;

	мЛог = Логирование.ПолучитьЛог("oscript.yadisk.uploader");
	мЛог.УстановитьУровень(УровниЛога.Предупреждение);

    мЛог.ДобавитьСпособВывода(ПолучитьФайлЖурнала());

	ПарсерКомСтроки = Новый ПарсерАргументовКоманднойСтроки();
	ПарсерКомСтроки.ДобавитьПараметрФлаг("-publish", "сделать файл публичным и вывести в консоль публичную ссылку на файл");
	ПарсерКомСтроки.ДобавитьПараметрФлаг("-debug", "выводить отладочную информацию");
	ПарсерКомСтроки.ДобавитьПараметр("ПутьКПараметрам", "путь к файлу на вашем компьютере, в котором прописаны пути");
	
	Параметры = ПарсерКомСтроки.Разобрать(АргументыКоманднойСтроки);

	Если ПустаяСтрока(Параметры["ПутьКПараметрам"]) Тогда
		Сообщить("Не указаны параметры!");
		ВывестиСправку();
		ЗавершитьРаботуСкрипта(1);
	КонецЕсли;
	
	ФайлСПараметрами = Новый Файл(Параметры["ПутьКПараметрам"]);
	
	Если НЕ ФайлСПараметрами.Существует() Тогда
		мЛог.Ошибка(СтрШаблон("Файл %1 не найден!", Параметры["ПутьКПараметрам"]));
		Сообщить(СтрШаблон("Файл %1 не найден!", Параметры["ПутьКПараметрам"]));
		ЗавершитьРаботуСкрипта(1);
	КонецЕсли;
	    
	Если Параметры["-debug"] Тогда
		мЛог.УстановитьУровень(УровниЛога.Отладка);
	КонецЕсли;

	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.Прочитать(Параметры["ПутьКПараметрам"]);
	ТекстФайла = ТекстДок.ПолучитьТекст();
	
	МассивПараметров = СтрРазделить(ТекстФайла, "<Забекапить>",Ложь);
	
	Для Каждого Элемент Из МассивПараметров Цикл

		Если СтрЧислоСтрок(Элемент) < 3 Тогда
			мЛог.Ошибка(СтрШаблон("В файл записаны не все параметры!", Параметры["ПутьКПараметрам"]));	
			Сообщить("В файл parameters.txt записаны не все параметры");
			ЗавершитьРаботуСкрипта(1);
		КонецЕсли;
	 
		ПутьКФайлу         = СтрПолучитьСтроку(СокрЛП(Элемент),1);
		ПутьКАрхиву        = СтрПолучитьСтроку(СокрЛП(Элемент),2);
		ПутьКФайлуНаДиске  = СтрПолучитьСтроку(СокрЛП(Элемент),3);
		МаксКолАрхивов     = СтрПолучитьСтроку(СокрЛП(Элемент),4);
		
		ИерархияПапокДиска = СтрРазделить(ПутьКФайлуНаДиске, "/",Ложь);
		РабочаяПапкаДиска  = ИерархияПапокДиска[ИерархияПапокДиска.ВГраница()];
		 
		ИмяБекапа = Лев(СтрЗаменить(ТекущаяДата(),".",""),8)+"_"+Лев(Прав(СтрЗаменить(ТекущаяДата(),":",""),6),4);
		ПутьКАрхиву = ПутьКАрхиву+"\"+ИмяБекапа+".zip";
		
		ЗаписьZIP =  Новый ЗаписьZipФайла(ПутьКАрхиву);     
	    ЗаписьZIP.Добавить(ПутьКФайлу+"\1Cv8.1CD");
	    ЗаписьZIP.Записать();		   
		
		СвойстваПапкиДиска = ПолучитьСвойстваПапкиДиска(ПутьКФайлуНаДиске);
		КоличествоАрхивовНаДиске = СвойстваПапкиДиска._embedded.total;
		
		Если Число(МаксКолАрхивов) <= Число(КоличествоАрхивовНаДиске) Тогда
			ОтобратьРанниеАрхивы(КоличествоАрхивовНаДиске - МаксКолАрхивов +1,СвойстваПапкиДиска._embedded);
		КонецЕсли;
		
		ЗагрузитьФайлНаЯндексДиск(ПутьКАрхиву, РабочаяПапкаДиска+"/"+ИмяБекапа+".zip");
	
	КонецЦикла;

	мЛог.Закрыть();

КонецПроцедуры

Процедура ВывестиСправку()
	Сообщить(
		"Использование: oscript yadisk-uploader.os [-debug|-d] path/to/parameters
		|	path/to/parameters      путь файлу с параметрами    parameters.txt
		|	-debug, -d 		вывести отладочную информацию"
	);
КонецПроцедуры

Процедура ЗагрузитьФайлНаЯндексДиск(ПутьКФайлу, ПутьКФайлуНаДиске)

	ЯндексДиск = Новый ЯндексДиск;
	ЯндексДиск.УстановитьРежимОтладки(мЛог.Уровень() = УровниЛога.Отладка);

    ПутьКФайлуНаДиске = "app:/" + ПутьКФайлуНаДиске; 
	 
    ЯндексДиск.УстановитьТокенАвторизации(ПолучитьТокенАвторизации());
	ЯндексДиск.ЗагрузитьНаДиск(ПутьКФайлу, ПутьКФайлуНаДиске);
	 
КонецПроцедуры

Функция ПолучитьПеременнуюСреды(ИмяПеременной)
    СистемнаяИнформация = Новый СистемнаяИнформация;
    Возврат СистемнаяИнформация.ПолучитьПеременнуюСреды(ИмяПеременной);
КонецФункции

Функция ПолучитьТокенАвторизации()
	ПутьКФайлуСТокеном = ОбъединитьПути(Новый Файл(ТекущийСценарий().Источник).Путь, "oauth_token.txt");
	Если (Новый Файл(ПутьКФайлуСТокеном)).Существует() Тогда
		ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлуСТокеном);
		Токен = СокрЛП(ЧтениеТекста.Прочитать());
		ЧтениеТекста.Закрыть();
		мЛог.Отладка("Токен получен из файла " + ПутьКФайлуСТокеном);
	КонецЕсли;
	Если ПустаяСтрока(Токен) Тогда
		мЛог.Предупреждение("Не найден токен авторизации!");
		мЛог.Информация("Токен авторизации необходимо расположить в файле oauth_token.txt в папке скрипта yadisk-uploader.os");
		мЛог.Отладка("Не найден токен авторизации: " + ПутьКФайлуСТокеном);
		ЗавершитьРаботуСкрипта(1);
	КонецЕсли;
	Возврат Токен;
КонецФункции

Процедура ЗавершитьРаботуСкрипта(КодВозврата)
	мЛог.Закрыть();
	ЗавершитьРаботу(КодВозврата);
КонецПроцедуры

Функция ПолучитьФайлЖурнала()

	ПутьКЛогу = ОбъединитьПути(ТекущийСценарий().Каталог, "uploader.log");

	ФайлЖурнала = Новый ВыводЛогаВФайл;
	ФайлЖурнала.ОткрытьФайл(ПутьКЛогу);
	
	Возврат ФайлЖурнала;

КонецФункции

Функция ПолучитьСвойстваПапкиДиска(ПутьКФайлуНаДиске)

	ЯндексДиск = Новый ЯндексДиск;
	ЯндексДиск.УстановитьТокенАвторизации(ПолучитьТокенАвторизации()); 
	Возврат ЯндексДиск.ПолучитьСвойстваРесурса(ПутьКФайлуНаДиске, Неопределено);
	
КонецФункции

Процедура ОтобратьРанниеАрхивы(КоличествоУдаляемых,СвойстваПапкиДиска)
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ДатаСоздания",,"ДатаСоздания");
	ТЗ.Колонки.Добавить("Путь",,"Путь");
	Для каждого Элемент Из СвойстваПапкиДиска.items Цикл
		//Сообщить(Дата(Элемент.created));
		ДатаЭлемента = Дата(СтрЗаменить(СтрЗаменить(СтрЗаменить(Лев(Элемент.created,19),"-",""),"T",""),":",""));		
		Стр = ТЗ.Добавить();
		Стр.ДатаСоздания = ДатаЭлемента;
		Стр.Путь = Элемент.path;
	КонецЦикла;
	ТЗ.Сортировать("ДатаСоздания");
	
	Пока КоличествоУдаляемых > 0 Цикл
	   УдалитьИзДиска(ТЗ[КоличествоУдаляемых-1].Путь);
	   КоличествоУдаляемых = КоличествоУдаляемых - 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьИзДиска(Путь)
   	ЯндексДиск = Новый ЯндексДиск;
   	ЯндексДиск.УстановитьТокенАвторизации(ПолучитьТокенАвторизации()); 
	ЯндексДиск.Удалить(Путь,Ложь,Истина); 
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////

ВыполнитьРаботу();